class Heap() {
    mPriority    := om;
    mValue       := om;
    mLeft        := om;
    mRight       := om;
    mCount       := 0;
    
    static {
        top := procedure() {
            return [mPriority, mValue];
        };
        insert := procedure(priority, value) {
            if (isEmpty()) {
                this.mPriority := priority;
                this.mValue    := value;
                this.mLeft     := Heap();
                this.mRight    := Heap();
                this.mCount    := 1;
                return;
            }
            this.mCount += 1;
            if (priority < mPriority) {                         
                if (mLeft.mCount > mRight.mCount) {
                    mRight.insert(mPriority, mValue);
                } else {
                    mLeft.insert(mPriority, mValue);
                }
                this.mPriority := priority;
                this.mValue    := value;
            } else {
                if (mLeft.mCount > mRight.mCount) { 
                    mRight.insert(priority, value);
                } else {
                    mLeft.insert(priority, value);
                }
            }
        };
        remove := procedure() {
            assert(mCount > 0, "mCount == 0");
            this.mCount -= 1;
            if (mLeft.isEmpty()) { 
                update(mRight); 
                return;
            } 
            if (mRight.isEmpty()) { 
                update(mLeft ); 
                return;
            }
            if (mLeft.mPriority < mRight.mPriority) {
                this.mPriority := mLeft.mPriority;
                this.mValue    := mLeft.mValue;
                mLeft.remove();
            } else {
                this.mPriority := mRight.mPriority;
                this.mValue    := mRight.mValue;
                mRight.remove();
            }
        };
        update := procedure(t) {
            this.mPriority := t.mPriority;
            this.mValue    := t.mValue;
            this.mLeft     := t.mLeft;
            this.mRight    := t.mRight;
            this.mCount    := t.mCount;
        };      
        isEmpty := [] |-> mCount == 0;

        f_str := [] |-> toString(0);
        toString := procedure(n) {
            if (isEmpty()) {
                return " " * n + "Nil";
            } else {
                return mLeft.toString(n + 4) + "\n" + 
                       " " * n + "<" + mValue + ", " + mPriority + ">\n" +
                       mRight.toString(n + 4);
            }
        };
    }
}

heapSort := procedure(A) {
    H := Heap();    
    for (x in A) {
        H.insert(x, x);
    }
    graph2Dot(H, "heap-example");
    S := [];
    while (!H.isEmpty()) {
        S += [ H.top()[1] ];
        H.remove();
    }
    return S;
};

demoHeapSort := procedure() {
    L := [ rnd({1 .. 99}) : n in [1 .. 50] ];
    S := heapSort(L);
    print(L);
    print(S);
};

// This function takes a heap together with a file name and converts
// this heap into a representation that is suitable for tool the dot.  Then
// dot is called to compute a graphical representation of the tree.  The
// tool dot is part of the graphviz tool set for graph visualization.
// This tool set is freely available for download at http://www.graphviz.org/.
graph2Dot := procedure(tree, file) {
    // Define and initialize static variable of class Heap
    Heap.sNodeId := 0;
    graph    := "digraph G {\n";
    graph    += "    ordering = out;";
    graph    += "    node [shape = record];\n";
    count    := 0;
    Children := {};
    NodeDict := {};
    assignIds(tree, count, NodeDict, Children);
    for (k in [1 .. Heap.sNodeId]) {
        L := Children[k];
        if (L != om && L != []) {
            graph += "    n$k$ -> " + join(["n$c$" : c in L], ",");
            graph += ";\n";
        }
    }
    for (k in [1 .. Heap.sNodeId]) {
        node := NodeDict[k];
        graph += "    n$k$ [label = \"$node.mPriority$\"];\n";
    }
    graph += "}\n";
    writeFile("$file$.dot", [ graph ]);
    run("dot -Tpdf $file$.dot -o $file$.pdf");
    run("open $file$.pdf");
};
// This function assigns numbers to all nodes in the tree represented
// by node.  The parameter count is the initial value of the counter.
assignIds := procedure(rw H, rw count, rw NodeDict, rw Children) {
    // do not assign node ids to empty nodes
    if (H.mCount == 0) {
        return;
    }
    Heap.sNodeId += 1;
    H.mNodeId := Heap.sNodeId;  // dynamically add new member variable
                                // (YES, we can do that!)
    NodeDict[H.mNodeId] := H;
    assignIds(H.mLeft , count, NodeDict, Children);
    assignIds(H.mRight, count, NodeDict, Children);
    L := [];
    if (H.mLeft.mCount > 0) {
        L += [ H.mLeft.mNodeId ];
    }
    if (H.mRight.mCount > 0) {
        L += [ H.mRight.mNodeId ];
    }
    Children[H.mNodeId] := L;
};

demoHeapSort();
    

